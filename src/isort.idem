type bool    = False | True
type 'a list = Nil (* [] *) | Cons (* :: *) of 'a * 'a list
type nat     = Z (* 0 *) | S (* n *) of nat

iso recover f = case i <->
  let (o, g) = f i in
  let (o, o') = (o, o) in
  (o, inv f (o', g))
in

iso rec insert lt = case
| (x, [])      <-> ([x], 0)
| (x, y :: ys) <->
  let (x :: y :: ys, n) = match (lt (y, x), ys) with
    | ((False, (y, x)), ys) <-> (x :: y :: ys, 0)
    | ((True, (y, x)), ys)  <->
      let (ys, n) = insert lt (x, ys) in (y :: ys, S n)
  in (x :: y :: ys, n)
in

iso rec isort' lt = case
| []      <-> ([], [])
| x :: xs <->
  let (xs, ns) = isort' lt xs in
  let (xs, n) = insert lt (x, xs) in (xs, n :: ns)
in

iso isort lt = recover {isort' lt} in

iso rec nat_lt = case
| (0, S n)   <-> (True, (0, S n))
| (S m, 0)   <-> (False, (S m, 0))
| (0, 0)     <-> (False, (0, 0))
| (S m, S n) <-> let (b, (m, n)) = nat_lt (m, n) in (b, (S m, S n))
in

isort nat_lt [10; 5; 5; 1; 2; 9; 7; 8; 0; 4; 6; 5; 6]

